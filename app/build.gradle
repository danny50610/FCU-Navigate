apply plugin: 'com.android.application'

project.archivesBaseName = "FCU-Navigate"
def buildNumber = System.getenv("BUILD_NUMBER") ?: "dev"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "iecs.fcu_navigate"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "1.0.$buildNumber"
    }

    signingConfigs {
        release {
            File releaseKeySettings = new File(".keystore/settings.properties")
            if(releaseKeySettings.exists()) {
                Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(releaseKeySettings))

                storeFile file(keyProps['storeFilePath'])
                storePassword keyProps['storePassword']
                keyAlias keyProps['keyAlias']
                keyPassword keyProps['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        if (variant.name == 'release') {
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                output.outputFile = new File(
                        outputFile.parent, project.archivesBaseName + '-' + defaultConfig.versionName + '.apk'
                )
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.google.guava:guava:18.0'
}
